plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.5'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'nebula.release' version '20.2.0'
}

group = 'com.demo'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

configurations {
	mockServer
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.kohsuke:github-api:1.327'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	mockServer 'org.wiremock:wiremock-standalone:3.13.0'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.named('bootJar') {
	archiveClassifier.set('boot')
}

// JVM Test Suite Plugin
// https://docs.gradle.org/current/userguide/jvm_test_suite_plugin.html
testing {
	suites {
		test {
			useJUnitJupiter()
		}
		integrationTest(JvmTestSuite) {
			dependencies {
				implementation project()
				implementation 'io.rest-assured:rest-assured:5.3.0'
				implementation 'org.springframework.boot:spring-boot-starter-test'
			}
		}
	}
}

tasks.register('mockServer') {
	doLast {
		exec {
			String jar = configurations.mockServer.asPath
			commandLine 'java', '-jar', jar, '--port', '9080', '--root-dir=mocks/wiremock', '--record-mappings'
		}
	}
}

nebulaRelease {
	releaseBranchPatterns = ['^main$']
}
